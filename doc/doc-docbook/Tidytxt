#! /usr/bin/perl

# $Cambridge: exim/doc/doc-docbook/Tidytxt,v 1.3 2007/08/29 13:37:28 ph10 Exp $

# Script to tidy up the output of w3m when it makes a text file. First we
# convert sequences of blank lines into a single blank line, to get everything
# uniform. Then we go through and insert blank lines before chapter and
# sections, also converting chapter titles to uppercase.

# We also have to do some character translation in the first pass. It seems
# that xmlto now generates Unicode in its HTML pages. This gives three problems:
# (1) It inserts the byte sequence C2 A0 (U+00A0) as a fixed-width space;
# (2) It inserts a whole slew of "box drawing" characters round the heading.
# (3) It uses U+25CF as its bullet character.

@lines = <>;

$lastwasblank = 0;
foreach $line (@lines)
  {
  $line =~ s/\x{c2}\x{a0}/ /g;
  $line =~ s/\x{e2}\x{94}[\x{80}-\x{bf}]/-/g;
  $line =~ s/\x{e2}\x{97}\x{8f}/*/g;

  if ($line =~ /^\s*$/)
    {
    $line = "" if $lastwasblank;
    $lastwasblank = 1;
    next;
    }
  $lastwasblank = 0;
  }

# Find start of TOC, uppercasing its title

for ($i = 0; $i < scalar @lines; $i++)
  {
  $lines[$i] = "TABLE OF CONTENTS\n" if $lines[$i] =~ /^Table of Contents/;
  last if $lines[$i] =~ /^1\. /;
  }

# Find start of first chapter

for ($i++; $i < scalar @lines; $i++)
  { last if $lines[$i] =~ /^1\. /; }

# Process the body. We can detect the starts of chapters and sections by
# looking for preceding and following blank lines, and then matching against
# the numbers.

$chapter = 0;
for (; $i < scalar @lines; $i++)
  {
  next if $lines[$i-1] !~ /^$/ || $lines[$i+1] !~ /^$/;

  # Start of chapter

  if ($lines[$i] =~ /^(\d+)\. / && $1 == $chapter + 1)
    {
    $chapter++;
    $section = 0;
    $lines[$i] = "\n\n" . ("=" x 79) . "\n" . uc($lines[$i]);
    }

  # Start of next section

  elsif ($lines[$i] =~ /^(\d+)\.(\d+) / && $1 == $chapter && $2 == $section + 1)
    {
    $section++;
    $lines[$i] = "\n$lines[$i]" . "-" x (length($lines[$i]) - 1) . "\n";
    }
  }

print @lines;

# End
